#library "KeyPresses.acs"
#include "zcommon.acs"

function void HudMessagePlainCharInstant(int a, int color, int x, int y)
{
    HudMessage(c:a; HUDMSG_PLAIN, 0, color, x, y, 1.0 / 35 + 1);
}

script "Client_KeyPress" OPEN CLIENTSIDE
{
    int buttons;
    int forward;
    int side;
    
    SetHudSize(320, 200, 0);
    
    while (true)
    {
        if (!PlayerIsSpectator(ConsolePlayerNumber()) && GetUserCVar(ConsolePlayerNumber(), "jm_keypresses"))
        {
            SetActivatorToPlayer(ConsolePlayerNumber());
        
            buttons = GetPlayerInput(-1, INPUT_BUTTONS);
            forward = GetPlayerInput(-1, INPUT_FORWARDMOVE);
            side = GetPlayerInput(-1, INPUT_SIDEMOVE);
            
            // Left
            if (side < 0)
                HudMessagePlainCharInstant('A', CR_GRAY, 0.1, 70.0);
            else
                HudMessagePlainCharInstant('A', CR_RED, 0.1, 70.0);
                
            // Right
            if (side > 0)
                HudMessagePlainCharInstant('D', CR_GRAY, 20.1, 70.0);
            else
                HudMessagePlainCharInstant('D', CR_RED, 20.1, 70.0);
                
            // Back
            if (forward < 0)
                HudMessagePlainCharInstant('S', CR_GRAY, 10.1, 70.0);
            else
                HudMessagePlainCharInstant('S', CR_RED, 10.1, 70.0);
                
            // Forward
            if (forward > 0)
                HudMessagePlainCharInstant('W', CR_GRAY, 10.1, 60.0);
            else
                HudMessagePlainCharInstant('W', CR_RED, 10.1, 60.0);
                
            // Jump
            if (buttons & BT_JUMP)
                HudMessagePlainCharInstant('J', CR_GRAY, 35.1, 60.0);
            else
                HudMessagePlainCharInstant('J', CR_RED, 35.1, 60.0);
                
            // Crouch
            if (buttons & BT_CROUCH)
                HudMessagePlainCharInstant('C', CR_GRAY, 35.1, 70.0);
            else
                HudMessagePlainCharInstant('C', CR_RED, 35.1, 70.0);
            
            SetActivator(0);
        }
        
        Delay(1);
    }
}