#library "KeyPresses.acs"
#include "zcommon.acs"

function void HudMessagePlainCharInstant(int a, int color, int x, int y)
{
    HudMessage(c:a; HUDMSG_PLAIN, 0, color, x, y, 1.0 / 35 + 1);
}

script "Client_KeyPress" OPEN CLIENTSIDE
{
    int buttons;
    int forward;
    int side;
    int x, y;
    
    SetHudSize(320, 200, 0);
    SetFont("KEYFONT");
    
    while (true)
    {
        if (!PlayerIsSpectator(ConsolePlayerNumber()) && GetCVar("jm_keypresses"))
        {
            SetActivatorToPlayer(ConsolePlayerNumber());
        
            buttons = GetPlayerInput(-1, INPUT_BUTTONS);
            forward = GetPlayerInput(-1, INPUT_FORWARDMOVE);
            side = GetPlayerInput(-1, INPUT_SIDEMOVE);
            
            x = GetCVar("jm_keypresses_x") << 16;
            y = GetCVar("jm_keypresses_y") << 16;
            
            // Left
            if (side < 0)
                HudMessagePlainCharInstant('A', CR_GRAY, x - 10.0 + 0.1, y);
            else
                HudMessagePlainCharInstant('A', CR_RED, x - 10.0 + 0.1, y);
                
            // Right
            if (side > 0)
                HudMessagePlainCharInstant('D', CR_GRAY, x + 10.1, y);
            else
                HudMessagePlainCharInstant('D', CR_RED, x + 10.1, y);
                
            // Back
            if (forward < 0)
                HudMessagePlainCharInstant('S', CR_GRAY, x + 0.1, y + 10.0);
            else
                HudMessagePlainCharInstant('S', CR_RED, x + 0.1, y + 10.0);
                
            // Forward
            if (forward > 0)
                HudMessagePlainCharInstant('W', CR_GRAY, x + 0.1, y - 10.0);
            else
                HudMessagePlainCharInstant('W', CR_RED, x + 0.1, y - 10.0);
                
            // Jump
            if (buttons & BT_JUMP)
                HudMessagePlainCharInstant('J', CR_GRAY, x + 15.1, y - 15.0);
            else
                HudMessagePlainCharInstant('J', CR_RED, x + 15.1, y - 15.0);
                
            // Crouch
            if (buttons & BT_CROUCH)
                HudMessagePlainCharInstant('C', CR_GRAY, x + 15.1, y + 15.0);
            else
                HudMessagePlainCharInstant('C', CR_RED, x + 15.1, y + 15.0);
                
            // Strafe
            if (buttons & BT_STRAFE)
                HudMessagePlainCharInstant('Z', CR_GRAY, x - 15.0 + 0.1, y - 15.0);
            else
                HudMessagePlainCharInstant('Z', CR_RED, x - 15.0 + 0.1, y - 15.0);
            
            SetActivator(0);
        }
        
        Delay(1);
    }
}