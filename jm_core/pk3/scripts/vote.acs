// Enables simple map restart/nextmap voting - REPLACE WITH VOTEDEF WHEN 3.0 IS RELEASED
//
// Author: 
// Hypnotoad
//
// License:
// Free for any purpose. I'm not liable for any damages.

#library "vote"
#include "zcommon.acs"

#import "jm2_cdwn.acs"

#define HUDID1 1500 //cant be 1 - 9, 960+, 732+, 27233, 412, 985, 986, 11142
#define HUDID2 1501
#define HUDID3 1502
#define HUDID4 1503

#define MAXTYPE 1

int plvote[64];
bool playervoted[64];
int votetimer = 0;
bool voteInProgress = FALSE;

script 981 (int type) NET
{
	if (voteInProgress)
		terminate;
	if (type > MAXTYPE)
		terminate;
	if (!PlayerInGame(PlayerNumber()))
	{
		print(s:"You must be in game to initiate a vote");
		Terminate;
	}
	if (timerTics() < 35*2 || timerstage < 2)
	{
		print(s:"You must wait at least 2 seconds after the map starts before voting");
		terminate;
	}
	if (PlayerCount() == 1)
	{
		delay(5);
		if (type == 0)
		{
			ACS_Execute(978,0);
			delay(1);
			clearMostHud();
			resetmap();
			delay(1);
			terminate;
		}
		else if (type == 1)
		{
			if (GetCVar("sv_noexit") == 0)
				Exit_Normal(0);
			else
			{
				consolecommand("nextmap");
			}
			delay(1);
			terminate;
		}
	}
	voteInProgress = true;
	clearVotes();
	SetHudSize(640, 480, 0);
	plvote[PlayerNumber()] = 1;
	playervoted[PlayerNumber()] = TRUE;
	print(s:"You voted \cdYES \c*(by default)");
	Setfont("bigfont");
    HudMessageBold(n:0, s:" has initiated a vote!";HUDMSG_PLAIN, HUDID4, CR_RED, 320.0, 50.0, 5000.0);
	SetActivator(-1);
	delay(35);
	Setfont("smallfont");
	if (type == 0)
	{
		HudMessageBold(s:"\ccDo you want to \cdrestart the map? \n \n \ccPuke \cd982 \ccfor \cdYES \n \n \ccPuke \cg983 \ccfor \cgNO";HUDMSG_PLAIN, HUDID1, CR_RED, 320.0, 100.0, 32.0);
		delay(35);
		acs_execute(977,0);
		acs_executewait(976,0, "Restart map", "Do not restart map", 1);
		acs_terminate(977,0);
	}
	else if (type == 1)
	{
		HudMessageBold(s:"\ccDo you want to \cdproceed to the next map? \n \n \ccPuke \cd982 \ccfor \cdYES \n \n \ccPuke \cg983 \ccfor \cgNO";HUDMSG_PLAIN, HUDID1, CR_RED, 320.0, 100.0, 32.0);
		delay(35);
		acs_execute(977,0);
		acs_executewait(976,0, "Go to next map", "Do not go to next map", 2);
		acs_terminate(977,0);
	}
	votetimer = 0;
	clearVotes();
	voteInProgress = false;
}

script 977 (void)
{
    delay(35);
    votetimer++;
    restart;
}

script 976 (int vote1, int vote2, int round)
{
    HudMessageBold(s:"";HUDMSG_PLAIN, HUDID3, CR_WHITE, 320.0, 200.0, 20.0 );
    HudMessageBold(s:"";HUDMSG_PLAIN, HUDID2, CR_WHITE, 320.0, 200.0, 20.0 );
    int result1 = 0;
    int result2 = 0;
    int novote = 0;
	bool voteFinished = true;
    SetHudSize(640, 480, 0);
    //print(d:result1,s:" ",d:result2,s:" ",d:novote,s:" ",d:votetimer,s:" ",d:playervoted[0],d:plvote[0][0]); //debug
    if (votetimer < 30)
    {
		Setfont("smallfont");
		HudMessageBold(s:"Collecting votes...";HUDMSG_PLAIN, HUDID2, CR_WHITE, 320.0, 200.0, 30.0 );
		for(int Pln = 0; Pln <64; Pln++)
		{
			if(!playeringame(pln))
			{
				plvote[pln] = 3;
				continue;
			}
			else if (playervoted[pln] == TRUE)
			{
				continue;
			}
			else
			{
				plvote[pln] = 0;
			}
			
		}
		
		int tempresult1 = 0;
		int tempresult2 = 0;
		int tempnovote = 0;
		
		for(int Pl = 0; Pl <64; Pl++)
		{
			if (plvote[Pl] == 0)
			{
				tempnovote++;
				voteFinished = false;
			}
			else if (plvote[Pl] == 1)
				tempresult1++;
			else if (plvote[Pl] == 2)
				tempresult2++;
		}
		if (!voteFinished)
		{
			if ( tempresult1 > (tempresult2 + tempnovote) || tempresult2 > (tempresult1 + tempnovote) )
				voteFinished = TRUE;
		}

		if (!voteFinished)
		{
			delay(1);
			restart;
		}
    }
    HudMessageBold(s:"Voting finished";HUDMSG_PLAIN, HUDID2, CR_WHITE, 320.0, 200.0, 20.0 );
    for(int P = 0; P <64; P++)
    {
        if (plvote[P] == 0)
            novote++;
        else if (plvote[P] == 1)
            result1++;
        else if (plvote[P] == 2)
            result2++;
    }
    if (novote >= result1 + result2)
    {
		clearVoteHud();
		HudMessageBold(s:"\n\n\nNot enough votes!";HUDMSG_PLAIN, HUDID3, CR_WHITE, 320.0, 200.0, 20.0 );
		terminate;
    }
    else if (result1 > result2)
    {
		clearVoteHud();
        HudMessageBold(s:"\n\n\n",s:vote1,s:" won the vote!";HUDMSG_PLAIN, HUDID3, CR_WHITE, 320.0, 200.0, 20.0 );
		delay(35*3);
		if (round == 1)
		{
			ACS_Execute(978,0);
			clearVoteHud();
			delay(1);
			clearMostHud();
			resetmap();
			delay(1);
			terminate;
		}
		else if (round == 2)
		{
			if (GetCVar("sv_noexit") == 0)
				Exit_Normal(0);
			else
			{
				consolecommand("nextmap");
			}
		}
    }
    else if (result2 > result1)
    {
		clearVoteHud();
        HudMessageBold(s:"\n\n\n",s:vote2,s:" won the vote!";HUDMSG_PLAIN, HUDID3, CR_WHITE, 320.0, 200.0, 20.0 );
		terminate;
    }
    else if (result1 == result2)
    {
		clearVoteHud();
		HudMessageBold(s:"\n\n\nDraw, defaulting to no action.";HUDMSG_PLAIN, HUDID3, CR_WHITE, 320.0, 200.0, 20.0 );
    }
    
}

script 982 (void) NET
{
	if (votetimer > 0)
	{
		plvote[PlayerNumber()] = 1;
		playervoted[PlayerNumber()] = TRUE;
		print(s:"You voted \cdYES");
	}
}

script 983 (void) NET
{
	if (votetimer > 0)
	{
		plvote[PlayerNumber()] = 2;
		playervoted[PlayerNumber()] = TRUE;
		print(s:"You voted \cdNO");
	}
}

script 978 (void) CLIENTSIDE
{
	clearMostHud();
}

function void clearVoteHud(void)
{
	HudMessageBold(s:"";HUDMSG_PLAIN, HUDID1, CR_WHITE, 320.0, 200.0, 20.0 );
	HudMessageBold(s:"";HUDMSG_PLAIN, HUDID2, CR_WHITE, 320.0, 200.0, 20.0 );
	HudMessageBold(s:"";HUDMSG_PLAIN, HUDID3, CR_WHITE, 320.0, 200.0, 20.0 );
	HudMessageBold(s:"";HUDMSG_PLAIN, HUDID4, CR_WHITE, 320.0, 200.0, 20.0 );
}

function void clearMostHud(void) //clear 1 - 100, 900+, 700+, 27233, 412, 985, 986, 11142
{
	for (int a = 0; a < 100; a++)
	{
		HudMessageBold(s:"";HUDMSG_PLAIN, a, CR_WHITE, 320.0, 200.0, 20.0 );
	}
	for (int b = 900; b < 999; b++)
	{
		HudMessageBold(s:"";HUDMSG_PLAIN, b, CR_WHITE, 320.0, 200.0, 20.0 );
	}
	for (int c = 690; c < 799; c++)
	{
		HudMessageBold(s:"";HUDMSG_PLAIN, c, CR_WHITE, 320.0, 200.0, 20.0 );
	}
	HudMessageBold(s:"";HUDMSG_PLAIN, 27233, CR_WHITE, 320.0, 200.0, 20.0 );
	HudMessageBold(s:"";HUDMSG_PLAIN, 412, CR_WHITE, 320.0, 200.0, 20.0 );
	HudMessageBold(s:"";HUDMSG_PLAIN, 985, CR_WHITE, 320.0, 200.0, 20.0 );
	HudMessageBold(s:"";HUDMSG_PLAIN, 986, CR_WHITE, 320.0, 200.0, 20.0 );
	HudMessageBold(s:"";HUDMSG_PLAIN, 11142, CR_WHITE, 320.0, 200.0, 20.0 );
	HudMessageBold(s:"";HUDMSG_PLAIN, 839, CR_WHITE, 320.0, 200.0, 20.0 );
	HudMessageBold(s:"";HUDMSG_PLAIN, 300, CR_WHITE, 320.0, 200.0, 20.0 );
}

function void clearVotes(void)
{
	for(int a = 0; a <64; a++)
    {
        playervoted[a] = FALSE;
        plvote[a] = 0;
    }
}

function int timerTics(void)
{
	return timer() - cdownOffset;
}