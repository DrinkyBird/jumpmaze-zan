// Enables simple map restart/nextmap voting - REPLACE WITH VOTEDEF WHEN 3.0 IS RELEASED
//
// Author: 
// Hypnotoad
//
// License:
// Free for any purpose. I'm not liable for any damages.

#library "vote"
#include "zcommon.acs"

#define MAX_PLAYERS 64

bool readyToReset[MAX_PLAYERS];

script "ReadyToReset" (void) NET
{
    int player = PlayerNumber();
    if (readyToReset[player] || !playeringame(player))
        terminate;
        
    readyToReset[PlayerNumber()] = true;
    printbold(n:0,s:"\c* declared themselves \cdready to reset.");
    
    if (checkReadyToReset(-1))
    {
        // Restart the map
        resetmap();
        
        // Again, absolutely no clue what this was for lol
        delay(1);
        terminate;
    }
}

// Checks if all players are ready to reset,
// pass in a player number to exclude from the count,
// otherwise pass in -1
function bool checkReadyToReset (int exclude)
{
    int readyCount = 0;
    int inGameCount = 0;
    for (int i = 0; i < MAX_PLAYERS; i++)
    {
        // exclude this player from the count
        // probably because they just voted to reset
        if (i == exclude)
            continue;
        
        if (playeringame(i))
        {
            inGameCount++;
            if (readyToReset[i])
                readyCount++;
        }
    }
    return readyCount == inGameCount;
}
