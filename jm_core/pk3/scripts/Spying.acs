#library "Spying.acs"
#include "zcommon.acs"

#libdefine MAX_PLAYERS 64
#libdefine MAX_PLAYER_NUMBER MAX_PLAYERS - 1
#libdefine MSB_2 0xC0000000
#libdefine LSB_6 0x0000003F

int packets[3];
int playerSpying[MAX_PLAYERS];

int tempTIDArray[MAX_PLAYERS];

///////////////////////////////////////////////////
//////////////////// Functions ///////////////////
/////////////////////////////////////////////////

// Return the player number of who a player is spying
function int GetPlayerSpying(int playerNum)
{
    // Store every player's TID in temporary array
    for (int i = 0; i < MAX_PLAYERS; i++)
    {
        SetActivatorToPlayer(i);
        tempTIDArray[i] = ActivatorTID();
        Thing_ChangeTID(0, UniqueTID());
    }
    
    SetActivator(CheckPlayerCamera(playerNum));
    int spyingPlayerNum = PlayerNumber();
    if (spyingPlayerNum == -1) // Are we spying a non-player?
        spyingPlayerNum = playerNum;
        
    // Reset player TIDs
    for (int j = 0; j < MAX_PLAYERS; j++)
    {
        SetActivatorToPlayer(j);
        Thing_ChangeTID(0, tempTIDArray[j]);
    }
    
    SetActivatorToPlayer(playerNum);
    
    return spyingPlayerNum;
}

// Initialise array so players are watching themselves
function void Init_SpyingArray(void)
{
    for (int i = 0; i < MAX_PLAYERS; i++)
    {
        playerSpying[i] = i;
    }
}

// A bitwise right shift that works properly
function int rshiftu(int n, int s)
{
    if (n & 0x80000000)
    {
        return (n & 0x7FFFFFFF) >> s | (1 << (31 - s));
    }
    return n >> s;
}

////////////////////////////////////////////////////////
//////////////////// Client Scripts ///////////////////
//////////////////////////////////////////////////////

script "Client_SpyingOpen" OPEN CLIENTSIDE
{
    Delay(5);

    int playerNum = ConsolePlayerNumber();
    int spyingPlayerNum;
    int spyingSpying; // Who the player we're spying is spying
    int watching;
    
    Init_SpyingArray();
    
    SetHudSize(640, 480, false);

    while (true)
    {
        while (!GetCvar("jm_showspys"))
            Delay(35);
            
        SetActivatorToPlayer(playerNum);
        
        spyingPlayerNum = GetPlayerSpying(playerNum);
        
        // Update the server with who we're spectating
        if (playerSpying[playerNum] != spyingPlayerNum)
        {
            NamedRequestScriptPuke("Server_UpdateSpying", spyingPlayerNum);
        }
        
        if (GetUserCVar(playerNum, "ShowSpying"))
        {
            // Are we spying on someone else?
            if (playerNum != spyingPlayerNum)
            {
                spyingSpying = playerSpying[spyingPlayerNum];
                // Is the player we're spying on also spying on someone else?
                if (spyingSpying != spyingPlayerNum && spyingSpying != playerNum)
                {
                    HudMessage(s:StrParam(n:spyingPlayerNum + 1), s:" is watching ", s:StrParam(n:spyingSpying + 1); HUDMSG_PLAIN, 0, CR_UNTRANSLATED, 0.1, 190.1, 0.25);
                }
            }
        
            // show who is spying on you
            watching = 0;
            for (int player = 0; player < MAX_PLAYERS; player++)
            {
                // We know you're watching yourself
                if (player == playerNum)
                {
                    continue;
                }
            
                if (playerSpying[player] == playerNum && CheckPlayerCamera(player) != -1) // CheckPlayerCamera hack to see if player is in server
                {
                    HudMessage(s:StrParam(n:player + 1); HUDMSG_PLAIN, 0, CR_UNTRANSLATED, 0.1, 210.1 + watching * 10.0, 0.25);
                    
                    watching++;
                }
            }
            
            if (watching)
            {
                HudMessage(s:"Watching You:"; HUDMSG_PLAIN, 0, CR_WHITE, 0.1, 200.1, 0.25);
            }
        }
        
        SetActivator(0);
        
        Delay(5);
    }
}

script "Client_UpdateArray" (int packet1, int packet2, int packet3) CLIENTSIDE
{
    // Activator is server
    SetActivatorToPlayer(ConsolePlayerNumber());
    
    // Get position in array
    int position = (rshiftu((packet1 & MSB_2), 26) | rshiftu((packet2 & MSB_2), 28) | rshiftu((packet3 & MSB_2), 30));
    position *= 15;
    
    // Store player numbers from first packet
    int shift = 24;
    for (int i = 0; i < 5; i++)
    {
        playerSpying[position] = (packet1 >> shift) & LSB_6;
    
        if (position == MAX_PLAYER_NUMBER)
        {
            terminate;
        }
    
        shift -= 6;
        position++;
    }
    
    // Store player numbers from second packet
    shift = 24;
    for (int j = 0; j < 5; j++)
    {
        playerSpying[position] = (packet2 >> shift) & LSB_6;
    
        if (position == MAX_PLAYER_NUMBER)
        {
            terminate;
        }
    
        shift -= 6;
        position++;
    }
    
    // Store player numbers from third packet
    shift = 24;
    for (int k = 0; k < 5; k++)
    {
        playerSpying[position] = (packet3 >> shift) & LSB_6;
    
        if (position == MAX_PLAYER_NUMBER)
        {
            terminate;
        }
    
        shift -= 6;
        position++;
    }
}

////////////////////////////////////////////////////////
//////////////////// Server Scripts ///////////////////
//////////////////////////////////////////////////////

script "Server_UpdateSpying" (int spyingPlayerNum) NET
{
    if (!GetCvar("jm_showspys"))
        terminate;

    playerSpying[PlayerNumber()] = spyingPlayerNum;
}

script "Server_InitSpyingArray" OPEN
{
    Init_SpyingArray();
}

script "Who is Spying Who" OPEN
{
    while (!GetCvar("jm_showspys"))
        Delay(35);
    
    Delay(5);
    
    int offset, shift, playerNum;
    
    while (true)
    {
        offset = 0;
        
        for (int chunk = 0; chunk < 5; chunk++)
        {
            packets[0] = 0;
            packets[1] = 0;
            packets[2] = 0;
            packets[0] |= (chunk << (26 + (2 * 0))) & MSB_2; // Set position of the packets
            packets[1] |= (chunk << (26 + (2 * 1))) & MSB_2; // Set position of the packets
            packets[2] |= (chunk << (26 + (2 * 2))) & MSB_2; // Set position of the packets
            
            for (int packet = 0; packet < 3; packet++)
            {
                shift = 24;
                
                for (int player = 0; player < 5; player++) // We can only cram 5 player numbers into an integer
                {
                    if (CheckPlayerCamera(player + offset) != -1) // CheckPlayerCamera hack to see if player is in server
                    {
                        playerNum = playerSpying[player + offset];
                    }
                    else
                    {
                        playerSpying[player + offset] = player + offset;
                        playerNum = player + offset;
                    }
                    packets[packet] |= playerNum << shift;
                    
                    shift -= 6;
                    
                    // Send final packets
                    if (player + offset == MAX_PLAYER_NUMBER)
                    {
                        ACS_NamedExecuteAlways("Client_UpdateArray", 0, packets[0], packets[1], packets[2]);
                        
                        restart;
                    }
                }
                
                offset += 5;
            }
            
            ACS_NamedExecuteAlways("Client_UpdateArray", 0, packets[0], packets[1], packets[2]);
        }
    }
}