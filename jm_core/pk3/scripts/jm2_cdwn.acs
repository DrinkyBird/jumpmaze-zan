#library "jm2_cdwn"
#include "zcommon.acs"
#import "map_skill.acs"
#import "constants.acs"
#import "jmgame.acs"

int timerstage = 0;
int cdownOffset;
int cdwn_freeze = 1;

script 851 OPEN
{
	cdownOffset = timer();
    if ( JmGameGetInitFlags() & JMGF_MANUAL_COUNTDOWN )
    {
		timerstage = 0;
		cdwn_freeze = 0;
    }
	else if ( getCvar( "jm_countdown" ) == 0) 
	{
		timerstage = 2;
		cdwn_freeze = 0;
	}
	else 
	{
		JmGameSetInitFlags( JMGF_MANUAL_TIMER );
		timerstage = 1;
	}
}

script "PlayCountdownSound" ( int which ) CLIENTSIDE {
	if ( getUserCvar( ConsolePlayerNumber(), "jm_countdownsnd" ) ) {
		if ( which == 0 )
			AmbientSound("MKCDOWN1", 127);
		else
			AmbientSound("MKCDOWN2", 127);
	}
}

script 852 OPEN {
    delay(1);
    if ( JmGameGetInitFlags() & JMGF_MANUAL_COUNTDOWN ) {
        cdwn_freeze = 0;
    } else {
        ACS_NamedExecuteAlways("JMCountdown_Run", 0);
    }
}

script "JMCountdown_Run" (void)
{
	if ( StrICmp( StrParam( n:PRINTNAME_LEVEL ), "TITLEMAP" ) == 0
		|| StrICmp( StrParam( n:PRINTNAME_LEVEL ), "WIN01" ) == 0
		|| StrICmp( StrParam( n:PRINTNAME_LEVEL ), "WIN02" ) == 0
		)
	{
		cdwn_freeze = 0;
		terminate;
	}
	if ( getCvar( "jm_countdown" ) == 1) 
	{
		if ( JmGameGetMapType() == 2 ) {
			timerstage = 2;
			JmGameStartTimer();
			cdwn_freeze = 0;
			terminate;
		}
		setfont("bigfont");
		delay(35*getCvar( "jm_countdown" )-1);
		ACS_NamedExecuteAlways( "PlayCountdownSound", 0, 0 );
		HudMessage(s:"On your marks!";HUDMSG_PLAIN, -300, CR_WHITE, 0.5, 0.4, 4.0);
		delay(35*getCvar( "jm_countdown" ));
		ACS_NamedExecuteAlways( "PlayCountdownSound", 0, 0 );
		HudMessage(s:"Get set!";HUDMSG_PLAIN, -300, CR_WHITE, 0.5, 0.4, 4.0);
		delay(35*getCvar( "jm_countdown" ));
		ACS_NamedExecuteAlways( "PlayCountdownSound", 0, 1 );
		HudMessage(s:"\cgGO!";HUDMSG_PLAIN, -300, CR_WHITE, 0.5, 0.4, 4.0);
		JmGameStartTimer();
		timerstage++;
	}
	cdwn_freeze = 0;
}

script 853 enter
{
	//setPlayerProperty( 0, 1, PROP_TOTALLYFROZEN );
	//delay(1);
	if (timerstage == 0)
	{
		ScriptWait (851);
	}
	if ( timerstage < 2 )
	{
		setPlayerProperty( 0, 1, PROP_TOTALLYFROZEN );
	}
    while ( timerstage < 2 )
	{
		delay(1);
	}
	setPlayerProperty( 0, 0, PROP_TOTALLYFROZEN );
}

script 854 RESPAWN
{
	if (timerstage == 0)
	{
		ScriptWait (851);
	}
	if ( timerstage < 2 )
	{
		setPlayerProperty( 0, 1, PROP_TOTALLYFROZEN );
	}
    while ( timerstage < 2 )
	{
		delay(1);
	}
	setPlayerProperty( 0, 0, PROP_TOTALLYFROZEN );
}

script 855 UNLOADING
{
	if ( getCvar( "jm_countdown" ) == 1) 
	{
		cdwn_freeze = 1;
		setPlayerProperty( 1, 1, PROP_TOTALLYFROZEN );
	}
}