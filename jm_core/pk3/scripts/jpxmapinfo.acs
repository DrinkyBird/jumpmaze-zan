// Library to print Jumpix-style map information after entering map
// Author: AC3 (M0rbid Desire)

#library "jpxinfo"
#include "zcommon.acs"
#import "scripts/constants.acs"
#import "scripts/utility.acs"
#import "scripts/timer.acs"
#import "scripts/luk.acs"
#import "scripts/jmgame.acs"
#import "scripts/ranker_hs.acs"
#import "scripts/ranker_solo.acs"

str Difficulty_String_Tab[6] = {"\cfVery Easy", "\cdEasy", "\chModerate", "\cgHard", "\cmVery Hard", "\ccVaried"};
str Difficulty_Color_Letter_Tab[6] = {"\cf", "\cd", "\ch", "\cg", "\cm", "\cc"};
int Difficulty_Color_Tab[6] = {CR_GOLD, CR_GREEN, CR_BLUE, CR_RED, CR_BLACK, CR_GRAY};

str MapName, Author, MapNumber, Difficulty_String, Difficulty_Color_Letter;
int Difficulty_Color;
bool Skip_Blinking;

//Set map information

//Dif: Difficulty level 
//     0- Very Easy (Yellow)
//     1- Easy (Green)
//     2- Moderate (Blue)
//     3- Hard (Red)
//     4- Very Hard (Black)
//     5- Varied (Gray)

//Name: Map Name;
//Auth: Map Author;
//Map:  Map Number (E.g. JPX01);

Function void SetJpxMapInfo(int Dif, str Name, str Auth, str Map) {
    Difficulty_String = Difficulty_String_Tab[Dif];
    MapName = Name;
    Author = Auth;
    MapNumber = Map;
    Difficulty_Color = Difficulty_Color_Tab[Dif];
    Difficulty_Color_Letter = Difficulty_Color_Letter_Tab[Dif];
    if ((GetChar(Name, 0) == '\\' && GetChar(Name, 1) == 'c') || Dif == 5)
    {
    	Skip_Blinking = true;	
    }
    
}

Function void ShowJpxMapInfo(void) {
    ACS_ExecuteAlways(501, 0, 0, 0, 0);
}

Script 501 (void)
{
    Delay(20);
    setfont("BIGFONT");
    HudMessage(s:MapName; HUDMSG_FADEINOUT, 1, CR_WHITE, 0.5, 0.5, 1.0, 0.7, 0.5);
    delay(35);
    
    if (!Skip_Blinking)
    {
        setfont("BIGFONT");
        HudMessage(s:MapName; HUDMSG_FADEINOUT, 2, Difficulty_Color, 0.5, 0.5, 0.7, 0.5, 0.5);
        delay(24);
    
        setfont("BIGFONT");
        HudMessage(s:MapName; HUDMSG_FADEINOUT, 4, CR_WHITE, 0.5, 0.5, 0.7, 0.5, 0.5);
        delay(24);
    }
    
    bool show_record = false;
    int message_time = 5.8;
    if (JmGameGetMapType() == JMG_MAP_SOLO && JrhIsSet() && !JrhIsAutoShowDetailsTable())
    {
        show_record = true;
        message_time = 7.8;
    }
    
    setfont("BIGFONT");
    HudMessage(s:MapName; HUDMSG_FADEINOUT, 5, Difficulty_Color, 0.5, 0.5, message_time, 0.7, 0.5);
    delay(24);
    
    SetFont("SMALLFONT");
    HudMessage(s:"Author: ", s:Author; HUDMSG_FADEINOUT, 6, CR_WHITE, 0.5, 0.55, 1.8, 0.4, 0.4);
    delay(35*2);
    
    SetFont("SMALLFONT");
    HudMessage(s:"Map: ", s:Difficulty_Color_Letter, s:MapNumber; HUDMSG_FADEINOUT, 7, CR_WHITE, 0.5, 0.55, 1.8, 0.4, 0.4);
    delay(35*2);
    
    SetFont("SMALLFONT");
    HudMessage(s:"Difficulty: ", s:Difficulty_String; HUDMSG_FADEINOUT, 8, CR_WHITE, 0.5, 0.55, 1.8, 0.4, 0.4);
    
    if (show_record)
    {
    	delay(35*2);
        setFont( "SMALLFONT" );
        hudMessage(s:"Record holder: \ca", a : _jrs_hs_author; HUDMSG_FADEINOUT, 9, CR_WHITE, 0.5, 0.55, 1.8, 0.4, 0.4);
    }
}
